buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.69', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

version = '1.12.2-2.6.16B'
group = 'team.cqr.cqrepoured'
archivesBaseName = 'Chocolate_Quest_Repoured'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    includedDependencies
    atDependencies
    buildDependencies

    implementation.extendsFrom(includedDependencies)
    implementation.extendsFrom(atDependencies)
}

repositories {
    // Github Releases
    ivy {
        url 'https://github.com'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() } 
    }

    // CurseForge
    maven { url 'https://www.cursemaven.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    includedDependencies 'meldexun:ASMUtil:1.0.0@jar'
    includedDependencies 'meldexun:ReflectionUtil:1.0.0@jar'

    atDependencies fg.deobf('curse.maven:geckolib-388172:4020277')

    atDependencies fg.deobf('curse.maven:ReachFix-556777:5543551')
    atDependencies fg.deobf('curse.maven:jei-238222:5101347')

    buildDependencies 'curse.maven:AncientWarfare2-224602:3293318'
    buildDependencies 'curse.maven:CodeChickenLib-242818:2779848'
    buildDependencies 'curse.maven:CubicChunks-292243:3186888'
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    def atDepFileTree = files(configurations.atDependencies.collect { zipTree(it) }).getAsFileTree()
    accessTransformer = files(
        'src/main/resources/META-INF/accesstransformer.cfg',
        atDepFileTree.matching { include 'META-INF/geckolib_at.cfg' },
        atDepFileTree.matching { include 'META-INF/jei_at.cfg' },
        atDepFileTree.matching { include 'META-INF/accesstransformer.cfg' }
    )

    runs {
        client {
            taskName 'runClient CQR 1.12'
            environment 'MC_VERSION', '1.12.2'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'fml.coreMods.load', 'team.cqr.cqrepoured.asm.CQRPlugin,meldexun.reachfix.asm.ReachFixPlugin'
        }

        server {
            taskName 'runServer CQR 1.12'
            environment 'MC_VERSION', '1.12.2'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'fml.coreMods.load', 'team.cqr.cqrepoured.asm.CQRPlugin,meldexun.reachfix.asm.ReachFixPlugin'
        }
    }
}

jar {
    from {
        configurations.includedDependencies.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'FMLCorePlugin': 'team.cqr.cqrepoured.asm.CQRPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLAT': 'accesstransformer.cfg'
    }
}

compileJava {
    classpath = sourceSets.main.compileClasspath + configurations.buildDependencies
}
